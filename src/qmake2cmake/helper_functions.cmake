function(get_name_of_path path result)
    # get basename of filepath or dirname of dirpath
    cmake_path(SET path NORMALIZE "${path}")
    string(REGEX REPLACE "(.)/$" "\\1" path "${path}")
    cmake_path(GET path FILENAME name)
    set(${result} "${name}" PARENT_SCOPE)
endfunction()

function(get_dst_of_src src dstdir result)
    get_name_of_path("${src}" name)
    cmake_path(APPEND dst "${dstdir}" "${name}")
    set(${result} "${dst}" PARENT_SCOPE)
endfunction()

macro(install_target_files)
    cmake_parse_arguments("ARG" "" "TARGET;DESTINATION" "SOURCES" ${ARGN})
    if(NOT ARG_TARGET)
        message(FATAL_ERROR "install_target_files: missing argument: TARGET")
    endif()
    if(NOT ARG_DESTINATION)
        message(FATAL_ERROR "install_target_files: missing argument: DESTINATION")
    endif()
    if(NOT ARG_SOURCES)
        message(FATAL_ERROR "install_target_files: missing argument: SOURCES")
    endif()
    foreach(src ${ARG_SOURCES})
        get_dst_of_src("${src}" "${ARG_DESTINATION}" dst)
        install(CODE "message(STATUS \"Installing: ${ARG_TARGET}: ${dst}\")")
        #install(CODE "message([[file(COPY \"${src}\" DESTINATION \"${ARG_DESTINATION}/\")]])")
        install(CODE "file(COPY \"${src}\" DESTINATION \"${ARG_DESTINATION}/\")")
    endforeach()
endmacro()

macro(install_target_command)
    cmake_parse_arguments("ARG" "" "TARGET;WORKING_DIRECTORY" "COMMAND" ${ARGN})
    if(NOT ARG_TARGET)
        message(FATAL_ERROR "install_target_files: missing argument: TARGET")
    endif()
    # ARG_DESTINATION is not used by qmake
    # TODO ARG_WORKING_DIRECTORY? pass WORKING_DIRECTORY to execute_process?
    if(NOT ARG_COMMAND)
        message(FATAL_ERROR "install_target_files: missing argument: COMMAND")
    endif()
    # escape hell ...
    set(args ${ARG_COMMAND})
    list(TRANSFORM args REPLACE [["]] [[\\"]])
    list(TRANSFORM args REPLACE "\n" "\\\\n")
    list(TRANSFORM args REPLACE "\r" "\\\\r")
    list(TRANSFORM args REPLACE "\t" "\\\\t")
    set(escargs) # empty list
    foreach(arg ${args})
        if(arg MATCHES "[ \\]")
            list(APPEND escargs "\"${arg}\"") # wrap in quotes
        else()
            list(APPEND escargs "${arg}")
        endif()
    endforeach()
    list(JOIN escargs " " arg_str)
    set(arg_str_esc "${arg_str}")
    string(REGEX REPLACE [[\\]] [[\\\\]] arg_str_esc "${arg_str_esc}")
    string(REGEX REPLACE [["]] [[\\"]] arg_str_esc "${arg_str_esc}")
    install(CODE "message(STATUS \"Running: ${ARG_TARGET}: ${arg_str_esc}\")")
    install(CODE "execute_process(COMMAND ${arg_str} COMMAND_ERROR_IS_FATAL ANY)")
endmacro()
